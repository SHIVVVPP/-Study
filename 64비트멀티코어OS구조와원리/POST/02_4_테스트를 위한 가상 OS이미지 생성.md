# 테스트를 위한 가상 OS 이미지 생성

최종 부트로더에서 섹터를 정상적으로 로드하는지 확인하기 위해
자신의 섹터 번호를 화면 위치에 대응시켜서 0~9까지 번호를 출력하게 해서
화면에 출력된 문자의 위치와 수를 확인하여 정상여부를 판단한다.

![image](https://user-images.githubusercontent.com/34773827/60766532-29758280-a0e6-11e9-9aa2-e7644fe4108e.png)



가상 OS 이미지의 크기는 적어도 수백 KB 정도는 되어야 한다.

> 책에서는 512KB(1024섹터) 크기로 정하였다.

수천 섹터나 되는 코드를 작성하기 위해서
NASM의 전처리기(Preprocessor)를 제공하므로 이러한 작업을 간단하게 처리할 수 있다.

NASM 전처리는 **매크로(Macro), 조건(Condition), 반복(Loop) 구문을 지원** 하며,
**변수 할당** 과 **값 지정** 또한 가능하다.

01.Kernel32 디렉터리를 만들어 가상 OS 소스 파일로 사용할 **VirtualOS.asm** 파일을 생성한다.

그 뒤 부트 로더 코드와 동일한 방식으로 레지스터 초기화를 수행한 뒤,
화면 2번째 라인의 가장 왼쪽 위치부터 숫자를 출력한다.

```assembly
[ORG 0x00]
[BITS 16]

SECTION .text

jmp 0x1000:START

SECTORCOUNT:		dw  0x0000	; 현재 실행중인 섹터 번호 저장
TOTALSECTORCOUNT:	equ 1024	; 가상 OS의 총 섹터 수
					; 최대 1152 섹터(0x90000Byte) 까지 가능
							

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;	코드 영역
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
START:
	mov ax, cs
	mov ds, ax
	mov ax, 0xB800
	
	mov es, ax
			
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;	각 섹터 별로 코드를 생성
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	%assign i	0
	%rep TOTALSECTORCOUNT
		%assign i i+1
		
		; 현재 실행 중인 코드가 포함된 섹터의 위치를 화면 좌표로 변환
		mov ax, 2
		
		mul word [ SECTORCOUNT ]
		mov si, ax
		
		mov byte [ es:si + ( 160 * 2 ) ], '0' + ( i % 10 )
		mov byte [ es:si + ( 160 * 2 ) + 1 ], 0xF4 ; 흰 배경에 빨간 글씨
		
		add word [ SECTORCOUNT ], 1 
		
		
		%if i == TOTALSECTORCOUNT
			
			jmp $
		%else
			jmp ( 0x1000 + i * 0x20 ): 0x0000
		%endif
		
		times ( 512 - ( $ - $$ ) % 512)	db 0x00
		
	%endrep
```

**%** 문자로 시작하는 것이 전처리 문이다.

- **%assign i 0** :

  i 라는 변수를 지정하고 0으로 설정, i = 0 과 동일

- **%rep  TOTALSECTORCOUNT** :

   %endrep까지의 라인을 TOTALSECTORCOUNT번 반복하여 삽입

- **%assign i i+1**

  i의 값을 1 증가, i = i + 1과 동일

- **%if i == TOTALSECTORCOUNT**

  i의 값이 TOTALSECTORCOUNT 와 같으면, 즉 마지막 섹터이면 아래의 라인을 삽입한다.

- **%else**

  위의 %if 조건이 일치하지 않았을 때 아래의 라인을 삽입

- **%endif**

  if 블록 종료

- times ( 512 - ( $ - $$ ) % 512 ) db 0x00 

  섹터의 상위에 화면 출력 코드를 삽입하고 512바이트 단위로 정렬하기 위해 남은영역을 0으로 채운다.

- **%endrep**

  rep 블록 종료

<hr>

### 실행화면

![image](https://user-images.githubusercontent.com/34773827/60768307-ffc85580-a0fd-11e9-9fdf-3289f1f8fc83.png)

