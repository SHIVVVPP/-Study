# 보호 모드로 전환

보호 모드로 전환하려면

- GDTR 레지스터 설정
- CR0 컨트롤 레지스터 설정
- jmp 명령 수행

3단계만 수행하면 된다.

<hr>



## 프로세서에 GDT 정보 설정

프로세서에 GDT 정보를 설정하는 것은 **lgdt 명령어** 를 사용한다.

**lgdt 명령어** 는 2바이트 크기와 4바이트 기준 주소로 된 GDT 정보 자료구조를 오퍼랜드로 받는다.

```assembly
lgdt [GDTR]	; GDTR 자료구조를 프로세서에 설정하여 GDT 테이블을 로드
```

<hr>



##  CR0 컨트롤 레지스터 설정

CR0 컨트롤 레지스터에는 보호 모드 전환에 관련된 필드 외에
캐시(Cache), 페이징(Paging), 실수 연산 장치(FPU) 등과 관련된 필드가 포함되어 있다.

< CR0 컨트롤 레지스터의 구조와 보호 모드 전환을 위한 설정 값 >

[![img](https://dongyeollee.github.io/images/OS/4/7.png)](https://dongyeollee.github.io/images/OS/4/7.png)

< CR0 컨트롤 레지스터의 필드와 의미 >

| 필드 | 설명                                                         |
| ---- | ------------------------------------------------------------ |
| PE   | - Protection Enable 의 약자로 보호 모드 진입 여부를 설정<br>- 1 로 설정하면 보호 모드로 진입하며, 0으로 설정하면 리얼모드로 진입한다. |
| MP   | - Monitor Coprocessor의 약자로 wait 또는 fwait 명령 실행 시 TS 필드 참고 여부를 설정한다.<br>- 1로 설정하면 wait또는 fwait 명령 실행시 TS 필드가 1이면 Device-not-available 예외가 발생<br>  0으로 설정하면 TS 필드의 값을 무시한다. |
| EM   | - Emulation의 약자로 프로세서에 FPU가 내장되었는지 여부를 설정한다.<br>- 1로 설정하면 FPU 관련 명령 실행시 Device-not-available 예외가 발생<br>   0으로 설정하면 정상적으로 실행된다.<br>-  FPU가 없으면 실수 연산 명령을 소프트웨어적으로 처리할 목적으로 사용한다. |
| TS   | - Task Switched의 약자로 태스크가 전환되었음을 나타낸다.<br />- 1로 설정하면 FPU 관련 명령 명령 실행 시 Device-not-available 예외가 발생하며<br/>  0으로 설정하면 FPU관련 명령을 정상적으로 실행한다.<br/>- EM 필드와 MP 필드와 조합하여 FPU의 상태를 저장하고 복구하는 용도로 사용한다. |
| ET   | - Extension Type의 약자로 1로 예약된다.<br/>- 과거 386, 486 프로세서에서 FPU를 지원한다는 것을 표시하는 용도로 사용되었다. |
| NE   | - Numeric Error의 약자로 FPU 에러 처리 여부를 내부 인터럽트 또는 외부 인터럽트 중 선택<br/>- 1로 설정하면 FPU 에러를 프로세서 내부의 예외로 연결하며,<br/>  0으로 설정하면 인터럽트로 연결한다. |
| WP   | - Write Protect의 약자로 어드레스 정렬 검사 기능을 사용할지 여부를 설정한다.<br/>- 1로 설정하면 상위 권한(0~2)의 코드가 유저 권한(3)으로 설정된 읽기 전용 페이지에 쓸 수 없으며,<br/>  0으로 설정하면 페이지 속성에 관계없이 쓸 수 있다. |
| AM   | - Alignment Mask의 약자로서, 쓰기 금지 기능을 사용할지 여부를 설정한다.<br/>- 1로 설정하면 데이터나 어드레스가 특정 값의 배수에서 시작하는지 체크하며,<br/>  0으로 설정하면 페이지 속성에 관계없이 쓸 수 있다. |
| NW   | - Not Write-through의 약자로 캐시 정책 중 Write-through를 사용할지 여부를 설정한다.<br/>- 1로 설정하면 Write-back 정책을 사용하며, 0으로 설정하면 Write-through 정책을 사용한다. |
| CD   | - Cache Disable의 약자로 프로세서의 캐시를 사용할지 여부를 설정한다.<br/>- 1로 설정하면 캐시를 사용하지 않으며, 0으로 설정하면 캐시를 사용한다. |
| PG   | - Paging의 약자로, 페이징 기능을 사용할지 여부를 설정한다.<br/>- 1로 설정하면 페이징 기능을 사용하며, 0으로 설정하면 페이징 기능을 사용하지 않는다. |

CR0 컨트롤 레지스터는 다양한 필드를 가지지만,
우리가 만드는 OS에서 보호 모드는 거쳐 가는 임시 모드이므로 세그멘테이션 기능 외에는 사용하지 않는다.

> 따라서 페이징, 캐시, 메모리 정렬 검사, 쓰기 금지 기능은 모두 사용하지 않음으로한다.
>
> FPU 역시 쓰지 않으므로 임시 값으로 설정한다.



#### 우리 OS에서의 CR0 레지스터 설정

```assembly
mov eax, 0x4000003B	; PG=0, CD=1, NW=0, AM=0, WP=0, NE=1, ET=1, TS=1, EM=0, MP=1, PE=1
mov cr0, eax		; CR0 컨트롤 레지스터에 위에서 저장한 플래그를 설정하여 보호모드로 전환
```

> CR0 컨트롤 레지스터가 32비트 크기이므로 32비트 크기의 범용 레지스터인 EAX를 사용한다.



<hr>



## 보호 모드로 전환과 세그먼트 셀렉터 초기화

이제 보호 모드로 전환하기 위한 모든 준비가 끝났다.
남은 것은 32비트 코드를 준비한 후, 
한 줄의 어셈블리어 코드로 CS 세그먼트 섹렉터(=레지스터)의 값을 바꾸는 것이다.

CS 세그먼트 셀렉터를 교체하려면 **jmp 명령** 과 **세그먼트 레지스터 접두사** 를 사용해야 한다.

**리얼모드의 세그먼트 레지스터** 는
세그먼트의 시작 어드레스(기준 주소)를 저장하는 레지스터이다.

**보호 모드의 세그먼트 레지스터** 는
리얼 모드와 달리 다양한 정보를 포함하고 있으므로 
세그먼트 정보는 디스크립터에 저장하고 세그먼트 셀렉터는 그 디스크립터를 지시하는 용도로 사용한다.



#### 보호모드에서 GDT 내의 디스크립터에 지시하는 방법

보호 모드도 마찬가지로 세그먼트 셀렉터에 어드레스를 설정하면 된다.
다만, 세그먼트의 기준 주소 대신 GDT 내의 디스크립터의 어드레스를 사용하며,
이는 **GDT의 시작 어드레스로부터 떨어진 거리(오프셋)** 을 의미한다.

> 예를들어 NULL 디스크립터 다음에 있는 커널 코드 세그먼트 디스크립터를 사용하고 싶다면,
>
> 크기가 8바이트이므로 0x08을 세그먼트 셀렉터에 넣으면 된다.
>
> 또는 GDT의 세 번째 위치한 커널 데이터 디스크립터를 사용하고 싶다면 0x10(=16)와 같이 사용하면 원하는 디스크립터를 지시할 수 있다.



### 보호 모드로 전환하는 코드

다음은 커널 코드 세그먼트를 사용하여 보호 모드로 전환하고 나서 나머지 세그먼트 셀렉터를 커널 데이터 세그먼트 디스크립터로 초기화 하는 코드이다.

```assembly
; 커널 코드 세그먼트를 0x00을 기준으로 하는 것으로 교체하고 EIP의 값을 0x00을 기준으로 재설정한다.
; CS 세그먼트 셀렉터 : EIP
jmp dword 0x08: ( PROTECTEDMODE - $$ + 0x10000 )
; 커널 코드 세그먼트가 0x00을 기준으로 하는 반면, 실제 코드는 0x10000을 기준으로 실행되고 있으므로,
; 오프셋에 0x10000을 더해서 세그먼트 교체 후에도 같은 선형 주소를 가리키게 한다.

[BITS 32]	; 이하의 코드는 32비트 코드로 설정한다.
PROTECTEDMODE:
	mov ax, 0x10	; 보호 모드 커널용 데이터 세그먼트 디스크립터를 AX 레지스터에 저장
	mov ds, ax		; DS 세그먼트 셀렉터에 설정
	mov es, ax		; ES 세그먼트 셀렉터에 설정
	mov fs, ax		; FS 세그먼트 셀렉터에 설정
	mov gs, ax		; GS 세그먼트 셀렉터에 설정
	
	; 스택을 0x00000000 ~ 0x0000FFFF 영역에 64KB 크기로 설정
	mov ss, ax		; SS 세그먼트 셀렉터에 설정
	mov esp, 0xFFFE	; ESP 레지스터의 어드레스를 0xFFFE로 설정
	mov ebp, 0xFFFE	; EBP 레지스터의 어드레스를 0xFFFE로 설정
```

이상으로 리얼모드에서 보호 모드로 전환하는 모든 과정이 끝나게 된다.

