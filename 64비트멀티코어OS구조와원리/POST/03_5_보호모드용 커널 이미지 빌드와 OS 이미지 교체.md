# 보호모드용 커널 이미지 빌드와 OS 이미지 교체

## 커널 엔트리 포인트 파일 생성

01.Kernel32 디렉터리의 Source 디렉터리 밑에 EntryPoint.s 파일을 추가

```assembly
[ORG 0x00]
[BITS 16]

SECTION .text

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;	코드 영역
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
START:
	mov ax, 0x1000
	
	mov ds, ax
	mov es, ax
	
	cli
	lgdt [ GDTR ]
	
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;	보호 모드로 진입
	;;	Disable Paging, Disable Cache, Internal FPU, Disable Align Check,
	;;	Enable ProtectedMode
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	mov eax, 0x4000003B
	mov cr0, eax
	
	jmp dword 0x08: ( PROTECTEDMODE - $$ + 0x10000 )
	
	
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;;	보호 모드로 진입
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
[BITS 32]
PROTECTEDMODE:
	mov ax, 0x10
	mov ds, ax
	mov es, ax
	mov fs, ax
	mov gs, ax
	
	; 스택을 0x00000000 ~ 0x0000FFFF 영역에 64KB 크기로 생성한다.
	mov ss, ax
	mov esp, 0xFFFE
	mov ebp, 0xFFFE
	
	; 화면에 보호 모드로 전환되었다는 메시지를 찍는다.
	push ( SWITCHSUCCESSMESSAGE - $$ + 0x10000 )
	push 0xF4; TODO 속성값
	push 2
	push 0
	call PRINTMESSAGE
	add esp, 12
	
	jmp $
	
	
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;	함수 코드 영역
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; 메시지를 출력하는 함수
; 스택에 X 좌표, Y 좌표, 속성값, 문자열
PRINTMESSAGE:
	push ebp
	mov ebp, esp
	push esi
	push edi
	push eax
	push ecx
	push edx
	
	
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;	X, Y의 좌표로 비디오 메모리의 어드레스를 계산한다.
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	 ; Y 좌표를 이용해서 먼저 라인 어드레스를 구함
    mov eax, dword [ ebp + 12 ] 
    mov esi, 160                
    mul esi                    
    mov edi, eax                
    
    ; X 좌료를 이용해서 2를 곱한 후 최종 어드레스를 구함
    mov eax, dword [ ebp + 8 ]  
    mov esi, 2                  
    mul esi                     
    add edi, eax                
                               
	
	
	; 출력할 문자열의 어드레스
	mov esi, dword [ ebp + 20 ]
	
.MESSAGELOOP:
	mov cl, byte [ esi ]
	
	cmp cl, 0
	je .MESSAGEEND
	
	mov byte [ edi + 0xB8000 ], cl
	
	add edi, 1	; EDI 레지스터에 1을 더하여 해당 문자에 속성값을 설정하는 위치로 이동한다.
	
	mov eax, dword [ ebp + 16 ]
	mov byte [ edi + 0xB8000 ], al
	
	add edi, 1	; EDI 레지스터에 1을 더하여 다음 문자 위치로 이동한다.
	add esi, 1	; ESI 레지스터에 1을 더하여 다음 문자열로 이동
	
	jmp .MESSAGELOOP
	
.MESSAGEEND:
    pop edx     
    pop ecx     
    pop eax    
    pop edi     
    pop esi     
	pop ebp     
    ret        
	
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;	데이터 영역
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; 아래의 데이터들을 8바이트에 맞춰 정렬하기 위해 추가
align 8, db 0

; GDTR의 끝을 8Byte로 정렬하기 위해 추가
dw 0x0000
; GDTR 자료구조 정의
GDTR:
	dw GDTEND - GDT - 1			; 아래에 위치하는 GDT 테이블의 전체 크기
	dd ( GDT - $$ + 0x10000 )	; 아래에 위치하는 GDT 테이블의 시작 어드레스
		
; GDT 테이블 정의
GDT:
	; 널 ( NULL ) 디스크립터
	NULLDescriptor:
		dw 0x0000
		dw 0x0000
		db 0x00
		db 0x00
		db 0x00
		db 0x00
			
		 ; 보호 모드 커널용 코드 세그먼트 디스크립터
    CODEDESCRIPTOR:     
        dw 0xFFFF       ; Limit [15:0]
        dw 0x0000       ; Base [15:0]
        db 0x00         ; Base [23:16]
        db 0x9A         ; P=1, DPL=0, Code Segment, Execute/Read
        db 0xCF         ; G=1, D=1, L=0, Limit[19:16]
        db 0x00         ; Base [31:24]  
			
		 ; 보호 모드 커널용 데이터 세그먼트 디스크립터
    DATADESCRIPTOR:
        dw 0xFFFF       ; Limit [15:0]
        dw 0x0000       ; Base [15:0]
        db 0x00         ; Base [23:16]
        db 0x92         ; P=1, DPL=0, Data Segment, Read/Write
        db 0xCF         ; G=1, D=1, L=0, Limit[19:16]
        db 0x00         ; Base [31:24]
GDTEND:

; 보호 모드로 전환되었다는 메시지
SWITCHSUCCESSMESSAGE: db 'Switch To Protected Mode Success!!', 0
	
times 512 - ( $ - $$ ) db 0x00
```

<hr>

## makefile 수정과 가상 OS 이미지 파일 교체

새로운 파일을 추가했으므로
추가된 파일을 빌드할 수 있게 makefile을 수정한다.

#### 01.Kernel32 디렉터리에 있는 makefile부터 수정한다.

< 01.Kernel32 디렉터리의 makefile >

```makefile
# 기본적으로 빌드를 수행할 목록
all: Kernel32.bin

# 커널 엔트리 포인트 빌드
Kernel32.bin: Source/EntryPoint.s
	nasm -o Kernel32.bin $<

# 소스 파일을 제외한 나머지 파일 정리	
clean:
	rm -f Kernel32.bin

```

**$<** 라고 표시된 기호는
Dependency(:의 오른쪽)의 첫 번째 파일을 의미하는 매크로이다.
따라서 **Source/EntryPoint.s** 로 치환되며, 이 엔트리 파일은 빌드되어 Kernel32.bin 파일로 생성된다.

<hr>

#### 최상위 디렉터리에 있는 makefile 수정

< 최상위 디렉터리의 makefile >

```makefile
# 기본적으로 빌드를 수행할 목록
all: BootLoader Kernel32 Disk.img

# 부트 로더 빌드를 위해 부트 로더 디렉터리에서 make 실행
BootLoader:
	@echo 
	@echo ============== Build Boot Loader ===============
	@echo 
	
	make -C 00.BootLoader

	@echo 
	@echo =============== Build Complete ===============
	@echo 
	
# 보호 모드 커널 이미지를 빌드하기 위해 보호 모드 디렉터리에서 make 실행
Kernel32:
	@echo 
	@echo ============== Build 32bit Kernel ===============
	@echo 
	
	make -C 01.Kernel32

	@echo 
	@echo =============== Build Complete ===============
	@echo 

	
# OS 이미지 생성
Disk.img: 00.BootLoader/BootLoader.bin 01.Kernel32/Kernel32.bin
	@echo 
	@echo =========== Disk Image Build Start ===========
	@echo 

	cat $^ > Disk.img

	@echo 
	@echo ============= All Build Complete =============
	@echo 
	
# 소스 파일을 제외한 나머지 파일 정리	
clean:
	make -C 00.BootLoader clean
	make -C 01.Kernel32 clean
	rm -f Disk.img	
```



#### 부트로더의  TOTALSECTORCOUNT 수정

빌드한 보호모드 커널 이미지의 크기는 512바이트 (1섹터) 이므로 한 섹터만 읽도록 수정한다.

```assembly
TOTALSECTORCOUNT: dw 1
```

<hr>



## OS 이미지 통합과 실행

#### 실행화면

![image](https://user-images.githubusercontent.com/34773827/60773877-07f8b300-a147-11e9-9bc3-ba68fe7a9cb9.png)

이상으로 보호 모드 전환에 대한 내용을 살펴보았다.

다음장에서는 C 코드와 어셈블리어를 함께 빌드하는것에 대해 다룬다.