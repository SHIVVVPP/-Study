# 프로세서 제조사와 IA-32e 지원 여부 검사

프로세서는 발전을 거듭하면서 수많은 기능이 추가된다.

IA-32e 모드도 그 중에 하나로 최근에 추가된 기능 중 하나(2004)이다.

프로세서마다 지원하는 기능이 조금씩 다르기 때문에 프로세서 제조사는 지원하는 기능을 확인할 수 있는 방법을 제공하며, 이것이 바로 CPUID 명령어이다.

<HR>

## CPUID를 사용하여 프로세서 정보를 확인하는 방법

x86 계열 프로세서는 프로세서에 대한 정보를 확인할 수 있는 CPUID 명령어를 제공하고 있다.

CPUID 명령어는 CPU Identification의 약자로 프로세서에 대한 다양한 정보를 제공한다.

> CPUID는 EAX 레지스터에 설정된 값에 따라 해당 정보를 조회하며,
> 범용 레지스터 EAX, EBX, ECX, EDX를 통해 그 결과를 넘겨준다.

CPUID는 크게 기본 CPUID 정보(Basic CPUID Information)와 확장 기능 CPUID 정보(Extended Feature CPUID Information)를 제공한다.

두 정보는 다시 여러가지 세부 정보로 나누어지는데
우리의 목적은 프로세서가 64비트 모드를 지원하는지 확인하는 것이므로 IA-32e 모드 부분을 위주로 살펴보자

< 제조사와 64비트 지원 기능에 관련된 CPUID 기능 목록 >

![image](https://user-images.githubusercontent.com/34773827/61028358-782b6100-a3f3-11e9-8ac3-97dda6688401.png)

위 표를 보면 알 수 있듯이 0x80000001으로 확장 기능 CPUID 정보를 조회하면,
EDX 레지스터의 비트 29로 64비트 지원 여부를 확인할 수 있다.

<hr>

## 프로세서 제조사와 IA-32e 모드 지원 여부 확인

CPUID는 EAX, EBX, ECX, EDX 레지스터로 값을 전달하므로 CPUID 명령어를 사용하려면
어셈블리어 코드로 직접 레지스터를 제어해야 한다.

따라서 이전에 보호 모드 엔트리 포인트를 위해 작성했던 PRINTMESSAGE 함수의 형태를 응용하여
C언어에서 호출할 수 있는 CPUID 어셈블리어 함수를 만들어 보자.

CPUID 명령어는 EAX 레지스터에서 조회할 정보를 넘겨받고,
조회된 정보를 EAX, EBX, ECX, EDX 레지스터에 담아 넘겨준다.

> 사실 어셈블리어 코드로 함수를 작성한다고 해서 C 코드에서 이를 호출할 수 있는 것은 아니다.
> 이를 위해서는 어셈블리어 코드에서 해당 함수가 외부에서 사용된다는 것을 알려야 하며,
> 이러한 역할을 하는 것이 global 지시어이다.
>
> 어셈블리어 파일 상단에 *global kReadCPUID* 와 같이 추가하면,
> 링크 단계에서 *kReadCPUID( )* 함수를 호출하는 부분과 어셈블리어 함수를 서로 연결해준다.

<hr>

다음은 DWORD 타입의 EAX값과 EAX, EBX, ECX, EDX의 어드레스를 넘겨받아 CPUID 명령어를 실행하는 어셈블리어 코드와 그 함수의 선언부를 나타낸 것이다.

> 주의해서 볼 부분은 스택에 들어 있는 두 번째 파라미터부터 다섯 번째 파라미터까지는 ESI 레지스터에 값을 옮긴 후, 다시 ESI 레지스터가 가리키는 어드레스에 값을 저장한다는 것이다.
>
> 이는 *kReadCPUID( )* 의 파라미터가 변수의 포인터이기 때문이다.
>
> 따라서 스택에 있는 값은 실제 변수의 어드레스이므로, ESI 레지스터에 어드레스를 옮긴 후 다시 ESI 레지스터가 가리키는 어드레스에 값을 저장하는 것이다.
>
> 이는 C언어의 '*pdwEAX = eax'와 같은 역할을 한다.

```assembly
; C 언어에서 호출할 수 있도록 이름을 노출(Export)
global kReadCPUID

SECTION .text

; CPUID 반환
; PARAM: DWORD dwEAX, DWORD* pdwEAX, *pdwEBX, *pdwECX, *pdwEDX
kReadCPUID:
	push ebp
	mov ebp, esp
	push eax
	push ebx
	push ecx
	push edx
	push esi
	
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;	EAX 레지스터의 값으로 CPUID 명령어 실행
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;	
	mov eax, dword [ ebp + 8 ]
	cpuid
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;	
	; 반환된 값을 파라미터에 저장
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;	
	; *pdwEAX
	mov esi, dword[ebp + 12]
	mov dword[esi], eax
	
	; *pdwEBX
	mov esi, dword[ebp + 16]
	mov dword[esi], ebx
	
	; *pdwECX
	mov esi, dword[ebp + 20]
	mov dword[esi], ecx
	
	; *pdwEDX
	mov esi, dword[ebp + 24]
	mov dword[esi], edx
	
	pop esi
	pop edx
	pop ecx
	pop ebx
	pop eax
	pop ebp
	ret
```

```c
// kReadCPUID() 함수 선언
void kReadCPUID(DWORD dwEAX, DWORD* pdwEAX, DWORD* pdwEBX, DWORD* pdwECX, DWORD* pdwEDX);
```

<hr>

 #### 함수를 통해 제조사 정보와 IA-32e 모드 지원 여부를 확인하는 C 코드 작성

제조사 정보는 표에서 볼수 있듯이 EAX 레지스터에 0x00을 넣은 후 CPUID를 호출하면
EBX,EDX,ECX 레지스터로부터 읽을 수 있다.

제조사 문자열은 하위 바이트에서 상위 바이트의 순서로 저장되므로, 이를 문자열 버포로 그대로 복사하면 하나의 완성된 문자열을 얻을 수 있다.

#####  *kReadCPUID( )* 함수를 사용하여 제조사 문자열을 결합하는 코드

```c
DWORD dwEAX, dwEBX, dwECX, dwEDX;
char vcVendorString[13] = {0,};

// 프로세서 제조사 정보 읽기
kReadCPUID( 0x00, &dwEAX, &dwEBX, &dwECX, &dwEDX);
*(DOWRD*)vcVendorString = dwEBX;
*((DWORD*)vcVendorString + 1) = dwEDX;
*((DWORD*)vcVendorString + 2) = dwECX;
// 제조사 문자열이 출력됨
kPrintString(0,0,/*attr*/,vcVendorString);
```

##### IA-32e 모드 지원 여부 검사 코드

```c
DWORD dwEAX, dwEBX, dwECX, dwEDX;

// 64비트 지원 여부 확인
kReadCPUID(0x80000001, &dwEAX, &dwEBX, &dwECX, &dwEDX);
if(dwEDX & (1<<29))
{
    kPrintString(0,0,/*Attr*/,"Pass");
}
else
{
    kPrintString(0,0,/*Attr*/,"Fail");
}
```

위 코드를 실행했을 때, Pass로 표시도니다면 IA-32e 모드를 지원하는 것이므로 전환을 수행하면 되며,
Fail로 표시된다면 실제 PC로는 테스트를 진행할 수 없지만,
QEMU 에뮬레이터로 테스트를 진행하면 된다.