# IA-32e 모드 전환과 1차 정리(2)



## 소스코드 1차 정리와 실행



### 모드 전환과 관련된 어셈블리어 코드 파일 추가

가장 먼저 할 작업은 초반에 살펴보았던 CPUID 명령어 및 IA-32e 모드 전환에 관한 어셈블리어 코드를 하나의 파일로 합치는 것이다.

어셈블리어 코드가 IA-32e 모드 전환에 관련된 함수이므로,
01.Kernel32/Source 디렉터리에 ModeSwitch.asm 과 ModeSwitch.h를 생성한다.

#### 01.Kernel32/Source/ModeSwitch.asm

```assembly
[BITS 32]

; c 언어에서 호출할 수 있도록 이름을 노출한다.(Export)
global kReadCPUID, kSwitchAndExecute64bitKernel

SECTION .text

; CPUID를 반환
;   PARAM: DWORD dwEAX, DWORD *pdwEAX, *pdwEBX, *pdwECX, *pdwEDX
kReadCPUID:
    push ebp
    mov ebp, esp
    push eax
    push ebx
    push ecx
    push edx
    push esi

    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    ;   EAX 레지스터의 값으로 CPUID 명령어 실행
    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    mov eax, dword [ebp+8]  ; 파라미터 1(dwEAX)를 EAX 레지스터에 저장
    cpuid                   ; CPUID 명령어 실행

    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    ; 반환된 값을 파라미터에 저장
    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    ; *pdwEAX
    mov esi, dword[ebp+12]  ; 파라미터 2(pdwEBX)를 ESI 레지스터에 저장
    mov dword[esi], eax     ; pdwEAX가 포인터이므로 포인터가 가리키는 어드레스에
                            ; EAX 레지스터의 값을 저장

    ; *pdwEBX
    mov esi, dword[ebp+16]  
    mov dword[esi], ebx

    ; *pdwECX
    mov esi, dword[ebp+20]
    mov dword[esi], ecx

    ; *pdwEDX
    mov esi, dword[ebp+24]
    mov dword[esi], edx

    pop esi
    pop edx
    pop ecx
    pop ebx
    pop eax
    pop ebp
    ret

; IA-32e 모드로 전환하고 64비트 커널을 수행
;   PARAM: 없음
kSwitchAndExecute64bitKernel:
    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    ;   CR4 레지스터의 PAE 비트를 1로 설정
    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    mov eax, cr4    ; CR4 컨트롤 레지스터의 값을 EAX 레지스터에 저장
    or eax, 0x20    ; PAE 비트(비트 5)를 1로 설정
    mov cr4, eax    ; PAE 비트가 1로 설정된 값을 CR4 컨트롤 레지스터에 저장

    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    ;   CR3 컨트롤 레지스터에 PML4 테이블의 어드레스와 캐시 활성화
    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    mov eax, 0x100000   ; EAX 레지스터에 PML4 테이블이 존재하는 0x100000(1MB)를 저장
    mov cr3, eax        ; CR3 컨트롤 레지스터에 0x100000(1MB)를 저장

    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    ;   IA32_EFER.LME를 1로 설정하여 IA-32e 모드를 활성화
    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    mov ecx, 0xC0000080 ; IA32_EFER MSR 레지스터의 어드레스를 저장
    rdmsr               ; MSR 레지스터를 읽기

    or eax, 0x0100      ; EAX 레지스터에 저장된 IA32_EFER MSR의 하위 32비트에서
                        ; LME 비트(비트8)을 1로 설정
    wrmsr               ; MSR 레지스터에 쓰기

    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    ;   CR0 컨트롤 레지스터를 NW 비트(비트 29) = 0, CD 비트(비트 30) = 0, PG 비트(비트 31) = 1로
    ;   설정하여 캐시 기능과 페이징 기능을 활성화
    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    mov eax, cr0        ; EAX 레지스터에 CR0 컨트롤 레지스터를 저장
    or eax, 0xE0000000  ; NW 비트(비트 29), CD 비트(비트 30), PG 비트(비트 31)을 모두 1로 설정
    xor eax, 0x60000000 ; NW 비트(비트 29)와 CD 비트(비트 30)을 XOR하여 0으로 설정
    mov cr0, eax        ; NW 비트 = 0, CD 비트 = 0, PG 비트 = 1로 설정한 값을 다시
                        ; CR0 컨트롤 레지스터에 저장

    jmp 0x08:0x200000   ; CS 세그먼트 셀렉터를 IA-32e 모드용 코드 세그먼트 디스크립터로 교체하고
                        ; 0x200000(2MB) 어드레스로 이동

    jmp $
```

<hr>

#### 01.Kernel32/Source/ModeSwitch.h

```c
#ifndef __UTILITY_H__
#define __UTILITY_H__

#include "Types.h"

void kReadCPUID(DWORD dwEAX, DWORD* pdwEAX,DWORD* pdwEBX, DWORD* pdwECX,DWORD* pdwEDX);
void kSwitchAndExecute64bitKernel(void);

#endif
```

<hr>



### 보호 모드 커널의 엔트리 포인트 파일 수정

보호 모드 커널의 엔트리 파일에는 IA-32e 모드 커널용 세그먼트 디스크립터가 추가되었다.

이 때문에 보호 모드 커널용 디스크립터의 위치가 변경되었으며, 
코드 세그먼트 디스크립터는 오프셋 0x08에서 0x18로,
데이터 세그먼트 디스크립터는 오프셋 0x10에서 0x20으로 옮겨졌다.

세그먼트 디스크립터의 위치가 변경되면 jmp 시 지정하는 코드 세그먼트 디스크립터와 모드 전환 후 설정하는 데이터 세그먼트 디스크립터를 변경해야 한다.

#### 수정한 보호 모드 커널의 엔트리 포인트 파일(01.Kernel32/Source/EntryPoint.s)

```assembly
[ORG 0x00]          ; 코드의 시작 어드레스를 0x00으로 설정
[BITS 16]           ; 이하의 코드는 16비트 코드로 설정

SECTION .text       ; text 섹션(세그먼트)을 정의

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;   코드 영역
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
START:
    mov ax, 0x1000  ; 보호 모드 엔트리 포인트의 시작 어드레스(0x10000)를 
                    ; 세그먼트 레지스터 값으로 변환
    mov ds, ax      ; DS 세그먼트 레지스터에 설정
    mov es, ax      ; ES 세그먼트 레지스터에 설정
    
    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    ; A20 게이트를 활성화
    ; BIOS를 이용한 전환이 실패했을 때 시스템 컨트롤 포트로 전환 시도
    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    ; BIOS 서비스를 사용해서 A20 게이트를 활성화
    mov ax, 0x2401          ; A20 게이트 활성화 서비스 설정
    int 0x15                ; BIOS 인터럽트 서비스 호출

    jc .A20GATEERROR        ; A20 게이트 활성화가 성공했는지 확인
    jmp .A20GATESUCCESS

.A20GATEERROR:
    ; 에러 발생 시, 시스템 컨트롤 포트로 전환 시도
    in al, 0x92     ; 시스템 컨트롤 포트(0x92)에서 1 바이트를 읽어 AL 레지스터에 저장
    or al, 0x02     ; 읽은 값에 A20 게이트 비트(비트 1)를 1로 설정
    and al, 0xFE    ; 시스템 리셋 방지를 위해 0xFE와 AND 연산하여 비트 0를 0으로 설정
    out 0x92, al    ; 시스템 컨트롤 포트(0x92)에 변경된 값을 1 바이트 설정
    
.A20GATESUCCESS:
    cli             ; 인터럽트가 발생하지 못하도록 설정
    lgdt [ GDTR ]   ; GDTR 자료구조를 프로세서에 설정하여 GDT 테이블을 로드

    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    ; 보호모드로 진입
    ; Disable Paging, Disable Cache, Internal FPU, Disable Align Check, 
    ; Enable ProtectedMode
    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    mov eax, 0x4000003B ; PG=0, CD=1, NW=0, AM=0, WP=0, NE=1, ET=1, TS=1, EM=0, MP=1, PE=1
    mov cr0, eax        ; CR0 컨트롤 레지스터에 위에서 저장한 플래그를 설정하여 
                        ; 보호 모드로 전환

    ; 커널 코드 세그먼트를 0x00을 기준으로 하는 것으로 교체하고 EIP의 값을 0x00을 기준으로 재설정
    ; CS 세그먼트 셀렉터 : EIP
    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    ;   변경 0x08 > 0x18
    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    jmp dword 0x18: ( PROTECTEDMODE - $$ + 0x10000 )

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; 보호 모드로 진입
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
[BITS 32]               ; 이하의 코드는 32비트 코드로 설정
PROTECTEDMODE:
    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    ;   변경 0x10 > 0x20
    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    mov ax, 0x20        ; 보호 모드 커널용 데이터 세그먼트 디스크립터를 AX 레지스터에 저장
    mov ds, ax          ; DS 세그먼트 셀렉터에 설정
    mov es, ax          ; ES 세그먼트 셀렉터에 설정
    mov fs, ax          ; FS 세그먼트 셀렉터에 설정
    mov gs, ax          ; GS 세그먼트 셀렉터에 설정
    
    ; 스택을 0x00000000~0x0000FFFF 영역에 64KB 크기로 생성
    mov ss, ax          ; SS 세그먼트 셀렉터에 설정
    mov esp, 0xFFFE     ; ESP 레지스터의 어드레스를 0xFFFE로 설정
    mov ebp, 0xFFFE     ; EBP 레지스터의 어드레스를 0xFFFE로 설정
    
    ; 화면에 보호 모드로 전환되었다는 메시지를 찍는다.
    push ( SWITCHSUCCESSMESSAGE - $$ + 0x10000 )    ; 출력할 메시지의 어드레스르 스택에 삽입
	push 0xA4
    push 2                                          ; 화면 Y 좌표(2)를 스택에 삽입
    push 0                                          ; 화면 X 좌표(0)를 스택에 삽입
    call PRINTMESSAGE                               ; PRINTMESSAGE 함수 호출
    add esp, 16                                     ; 삽입한 파라미터 제거

    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    ;   변경 0x08 > 0x18
    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    jmp dword 0x18: 0x10200 ; C 언어 커널이 존재하는 0x10200 어드레스로 이동하여 C 언어 커널 수행


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;	함수 코드 영역
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; 메시지를 출력하는 함수
; 스택에 X 좌표, Y 좌표, 속성값, 문자열
PRINTMESSAGE:
	push ebp
	mov ebp, esp
	push esi
	push edi
	push eax
	push ecx
	push edx
	
	
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	;;	X, Y의 좌표로 비디오 메모리의 어드레스를 계산한다.
	;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	 ; Y 좌표를 이용해서 먼저 라인 어드레스를 구함
    mov eax, dword [ ebp + 12 ] 
    mov esi, 160                
    mul esi                    
    mov edi, eax                
    
    ; X 좌료를 이용해서 2를 곱한 후 최종 어드레스를 구함
    mov eax, dword [ ebp + 8 ]  
    mov esi, 2                  
    mul esi                     
    add edi, eax                
                               
	
	
	; 출력할 문자열의 어드레스
	mov esi, dword [ ebp + 20 ]
	
.MESSAGELOOP:
	mov cl, byte [ esi ]
	
	cmp cl, 0
	je .MESSAGEEND
	
	mov byte [ edi + 0xB8000 ], cl
	
	add edi, 1	; EDI 레지스터에 1을 더하여 해당 문자에 속성값을 설정하는 위치로 이동한다.
	
	mov eax, dword [ ebp + 16 ]
	mov byte [ edi + 0xB8000 ], al
	
	add edi, 1	; EDI 레지스터에 1을 더하여 다음 문자 위치로 이동한다.
	add esi, 1	; ESI 레지스터에 1을 더하여 다음 문자열로 이동
	
	jmp .MESSAGELOOP
	
.MESSAGEEND:
    pop edx     
    pop ecx     
    pop eax    
    pop edi     
    pop esi     
	pop ebp     
    ret        
	
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;	데이터 영역
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; 아래의 데이터들을 8바이트에 맞춰 정렬하기 위해 추가
align 8, db 0

; GDTR의 끝을 8Byte로 정렬하기 위해 추가
dw 0x0000
; GDTR 자료구조 정의
GDTR:
	dw GDTEND - GDT - 1			; 아래에 위치하는 GDT 테이블의 전체 크기
	dd ( GDT - $$ + 0x10000 )	; 아래에 위치하는 GDT 테이블의 시작 어드레스
		
; GDT 테이블 정의
GDT:
	; 널 ( NULL ) 디스크립터
	NULLDescriptor:
		dw 0x0000
		dw 0x0000
		db 0x00
		db 0x00
		db 0x00
		db 0x00
    
    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    ;   추가: IA-32e 모드 커널용 코드 세그먼트 디스크립터
    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    ;   IA-32e 모드 커널용 코드 세그먼트 디스크립터
    IA_32eCODEDESCRIPTOR:
        dw 0xFFFF       ; Limit[15:0]
        dw 0x0000       ; Base[15:0]
        db 0x00         ; Base[23:16]
        db 0x9A         ; P=1, DPL=0, Code Segment, Execute/Read
        db 0xAF         ; G=1, D=0, L=1, Limit[19:16]
        db 0x00         ; Base[31:24]
    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    ;   추가: IA-32e 모드 커널용 데이터 세그먼트 디스크립터
    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
    ;   IA-32e 모드 커널용 데이터 세그먼트 디스크립터
    IA_32eDATADESCRIPTOR:
        dw 0xFFFF       ; Limit[15:0]
        dw 0x0000       ; Base[15:0]
        db 0x00         ; Base[23:16]
        db 0x92         ; P=1, DPL=0, Data Segment, Read/Write
        db 0xAF         ; G=1, D=0, L=1, Limit[19:16]
        db 0x00         ; Base[31:24]
			
		 ; 보호 모드 커널용 코드 세그먼트 디스크립터
    CODEDESCRIPTOR:     
        dw 0xFFFF       ; Limit [15:0]
        dw 0x0000       ; Base [15:0]
        db 0x00         ; Base [23:16]
        db 0x9A         ; P=1, DPL=0, Code Segment, Execute/Read
        db 0xCF         ; G=1, D=1, L=0, Limit[19:16]
        db 0x00         ; Base [31:24]  
			
		 ; 보호 모드 커널용 데이터 세그먼트 디스크립터
    DATADESCRIPTOR:
        dw 0xFFFF       ; Limit [15:0]
        dw 0x0000       ; Base [15:0]
        db 0x00         ; Base [23:16]
        db 0x92         ; P=1, DPL=0, Data Segment, Read/Write
        db 0xCF         ; G=1, D=1, L=0, Limit[19:16]
        db 0x00         ; Base [31:24]
GDTEND:

; 보호 모드로 전환되었다는 메시지
SWITCHSUCCESSMESSAGE: db 'Switch To Protected Mode Success!!', 0
	
times 512 - ( $ - $$ ) db 0x00
```

<hr>

### 보호 모드 커널의 *C 언어*  엔트리 포인트 파일 수정

IA-32e 모드 지원 여부를 검사하는 기능과 IA-32e 모드로 전환하는 기능이 추가되었으니
보호 모드 커널의 C 언어 엔트리 포인트 함수에서 이를 호출해야 한다.

한가지 문제가 있는데
IA-32e 모드 커널이 존재한다면 IA-32e 모드 전환 함수를 호출할 수 있지만,
아직 준비중이므로 IA-32e 모드 전환 함수를 호출할 수 없다.

그러므로 IA-32e 모드 전환 함수는 코드를 넣어두되 주석으로 처리해놓는다.

#### IA-32e 모드 지원여부 검사와 IA-32e 모드 전환 함수 추가(01.Kernel32/Source/Main.c)

```c
#include "Types.h"
#include "Page.h"
//----------------------------------------------------------------------------------
//  추가
//----------------------------------------------------------------------------------
#include "ModeSwitch.h"

void kPrintString( int iX, int iY, BYTE Attr, const char* pcString);
BOOL kInitializeKernel64Area(void);
BOOL kIsMemoryEnough( void );

//Main
void Main(void)
{
	 DWORD i;
//----------------------------------------------------------------------------------
//  추가
//----------------------------------------------------------------------------------
    DWORD dwEAX, dwEBX, dwECX, dwEDX;
    char vcVendorString[13] = {0,};
//----------------------------------------------------------------------------------
    
    kPrintString( 0, 3,0xE9, "C Language Kernel Start.....................[Pass]" );
    
    // 최소 메모리 크기를 만족하는 지 검사
    kPrintString( 0, 4,0x0A, "Minimum Memory Size Check...................[    ]" );
    if( kIsMemoryEnough() == FALSE )
    {
        kPrintString( 45, 4,0x4A, "Fail" );
        kPrintString( 0, 5,0xC1, "Not Enough Memory~!! CYNOS64 OS Requires Over "
                "64Mbyte Memory~!!" );
        while( 1 ) ;
    }
    else
    {
        kPrintString( 45, 4,0xA9, "Pass" );
    }
    
    // IA-32e 모드의 커널 영역을 초기화
    kPrintString( 0, 5,0x0A, "IA-32e Kernel Area Initialize...............[    ]" );
    if( kInitializeKernel64Area() == FALSE )
    {
        kPrintString( 45, 5,0x4A, "Fail" );
        kPrintString( 0, 6,0xC1, "Kernel Area Initialization Fail~!!" );
        while( 1 ) ;
    }
    kPrintString( 45, 5,0xA9, "Pass" );

	// IA-32e 모드 커널을 위한 페이지 테이블 생성
	kPrintString(0,6,0xB9,"IA-32e Page Tables Initialize...............[    ]");
	kInitializePageTables();
	kPrintString(45,6,0xA9,"Pass");

//----------------------------------------------------------------------------------
//  추가
//----------------------------------------------------------------------------------
    // 프로세서 제조사 정보 읽기
    kReadCPUID( 0x00, &dwEAX, &dwEBX, &dwECX, &dwEDX);
    *(DWORD*) vcVendorString = dwEBX;
    *((DWORD*)vcVendorString+1) = dwEDX;
    *((DWORD*)vcVendorString+2) = dwECX;
    kPrintString(0,7,0xF9,"Processor Vendor String.....................[            ]");
    kPrintString(45, 7, 0xA9, vcVendorString);

    // 64비트 지원 유무 확인
    kReadCPUID(0x80000001, &dwEAX,&dwEBX,&dwECX,&dwEDX);
    kPrintString(0,8,0xF9,"64bit Mode Support Check....................[    ]");
    if(dwEDX & (1 << 29))
    {
        kPrintString(45,8,0xA9,"Pass");
    }
    else
    {
        kPrintString(45,8,0x4A,"Fail");
        kPrintString(0,9,0x4E,"This Processor does not support 64bit mode!");
        while(1);
    }

    // IA-32e 모드로 전환
    kPrintString(0,9,0xF1,"Switch To IA-32e Mode");
    // 원래는 아래 함수를 호출해야 하나 IA-32e 모드 커널이 없으므로 주석처리
    // kSwitchAndExecute64bitKernel();
    
    while( 1 ) ;
}
```

<hr>

### 빌드와 실행

![image](https://user-images.githubusercontent.com/34773827/61035513-3fdf4f00-a402-11e9-89bb-34f8ea0df0f2.png)

