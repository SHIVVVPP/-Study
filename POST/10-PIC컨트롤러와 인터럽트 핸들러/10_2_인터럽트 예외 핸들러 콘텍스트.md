# 인터럽트, 예외 핸들러, 콘텍스트

```c
// 임시 예외나 인터럽트 핸들러
void kDummyHandler(void)
{
    kPrintString(0,0,0xAD,"======================================================");
    kPrintString(0,1,0xA4,"            Dummy Interrupt Handler Called~!          ");
    kPrintString(0,2,0xA4,"                   Interrupt Occur~!!                 ");
    kPrintString(0,3,0xAD,"======================================================");
    
    while(1);
}
```

이 전 장에서는 예외와 인터럽트를 모두 임시 핸들러를 등록하여 처리하였다.

그리고 임시 핸들러에서 예외나 인터럽트가 발생하기 전의 코드로 돌아가지 않고 무한 루프를 수행하여 그자리에 멈추게 하였는데,

무한 루프를 수행한 이유는 돌아갈 수 없었기 때문이다.

이번 절에서는 다시 코드로 돌아갈 수 없었던 이유와 다시 돌아가려면 무엇을 해야하는지 알아본다.

<hr>

## 임시 핸들러의 문제점

임시 핸들러에는 **크게 두 가지 문제가 있다.**

1. **인터럽트에서 복귀할 때 사용하는 명령어를 사용하지 않았다.**

   인터럽트나 예외가 발생하면 프로세서는 수행중인 코드로 복귀하는 데 필요한 기본 정보를 핸들러의 스택에 삽입한다.

   핸들러 수행 후 코드로 복귀하려면 저장된 데이터를 프로세서에 다시 복원해야 하는 데, <br>이러한 작업을 수행하려면 IRET 명령어를 명시적으로 사용해야 한다.

2. **프로세서의 상태를 완전히 저장 및 복원하지 않았다.**

   프로세서는 인터럽트 또는 예외 발생 시 자동으로 저장하는 레지스터 외에도 많은 레지스터가 있다.<br>이러한 레지스터들은 프로세서가 별도로 관리하지 않으므로 핸들러에서 이를 처리해야 한다.

   레지스터를 제대로 관리하지 않는다면 핸들러를 수행하는 동안 변경된 레지스터 때문에 코드가 정상적으로 실행되지 않는다.

<hr>

## 콘텍스트 저장과 복원

인터럽트 또는 예외가 발생했을 때, 핸들러를 수행한 후 코드로 복귀하려면<br> 프로세서의 상태를 저장하고 복원해야 한다.

프로세서는 레지스터를 기반으로 코드를 수행하므로 프로세서의 상태는 곧 코드 수행에 관계된 레지스터의 집합이라고 할 수 있다.

이렇게 **프로세서 상태와 관계된 레지스터의 집합을 다른말로 콘텍스트(Context)** 라고 부른다.

<hr>

인터럽트 또는 예외로 인해 핸들러가 수행되거나 어떤 이유로 현재 수행중인 코드를 중단하고 나서 다시 수행해야 한다면 **전후의 콘텍스트를 동일하게 유지해야 한다**.

콘텍스트를 유지하는 방법은 간단하다.

**콘텍스트를 위한 메모리 공간을 할당한 뒤, 정해진 순서대로 레지스터를 저장하고 복원하면 된다.**

우리 OS는 콘텍스트 처리를 위해 범용 레지스터 부터 세그먼트 셀렉터의 순서로 핸들러의 스택에 콘텍스트를 저장하는 방식을 사용한다.

< 우리 OS에서 콘텍스트를 저장하고 복원하는 순서 >

실수 연산에 관련된 FPU 레지스터를 제외한 거의 모든 레지스터를 저장한다.

![image](https://user-images.githubusercontent.com/34773827/61195740-0d389d80-a705-11e9-95a8-f87fc045aa5b.png)

<hr>

### 콘텍스트를 저장하고 복원하는 코드

그림에서 "프로세서가 처리하는 부분"은 프로세서가 처리하는 부분이므로<br>이 부분을 제외한 나머지 부분을 저장하고 복원하도록한다.

```assembly
ISRKeyboard:
	; 콘텍스트 저장
	; RBP 레지스터부터 GS 세그먼트 셀렉터까지 모두 스택에 삽입
	push rbp
	mov rbp, rsp
	push rax
	push rbx
	push rcx
	push rdx
	push rdi
	push rsi
	push r8
	push r9
	push r10
	push r11
	push r12
	push r13
	push r14
	push r15
	
	mov ax, ds
	push rax
	mov ax, es
	push rax
	push fs
	push gs
	
	; 세그먼트 셀렉터 교체
	mov ax, 0x10
	mov ds, ax
	mov es, ax
	mov gs, ax
	mov fs, ax
	
	; C로 작성된 핸들러 함수 호출
	call kKeyboardHandler
	
	;콘텍스트 복원
	;GS 세그먼트 셀렉터부터 RBP 레지스터까지 모두 스택에서 꺼내서 복원
	pop gs
	pop fs
	pop rax
	mov es, ax
	pop rax
	mov ds, ax
	
	pop r15
	pop r14
	pop r13
	pop r12
	pop r11
	pop r10
	pop r9
	pop r8
	pop rsi
	pop rdi
	pop rdx
	pop rcx
	pop rbx
	pop rax
	pop rbp
	
	; 스택에 에러 코드가 포함되어 있다면 제거
	add rsp, 8
	
	; 프로세서가 저장한 콘텍스트를 복원하여 실행 중인 코드로 복귀
	iretq
```

인터럽트나 예외가 발생했을 때 프로세서가 하는 역할은 단순히 IST 스택에 콘텍스트의 일부를 저장하고 복원하는 것 뿐이다. 다시 말하면 프로세서는 복원하는 콘텍스트가 이전에 자신이 저장한 콘텍스트와 같은지 비교하지는 않는다.

이것은 인터럽트나 예외가 발생했을 때 저장한 콘텍스트가 아닌 다른 콘텍스트로 복원할 수 있음을 뜻하는데,

만일 콘텍스트 영역을 여러 개 만들고, 인터럽트가 발생했을 대 이들을 순차적으로 교환한다면 소프트웨어 멀티태스킹을 구현할 수 있다.

> 멀티 태스킹이란 여러 개의 태스크(작업)를 교대로 실행하여 마치 동시에 여러 개의 프로그램이 실행되는 것과 같은 효과를 내는 것이다.

<hr>



## 인터럽트와 예외 핸들러 업그레이드

이전 장에서는 테스트를 위해 간단히 작성한 임시 핸들러를 모든 IDT 테이블에 등록했다.

이번 절에서는 인터럽트와 예외에 따라 핸들러를 개별적으로 등록하여, 인터럽트와 예외를 구분하여 처리하도록 해보자.

> 인터럽트와 예외처리 방법은 스택에 삽입된 에러 코드만 제외하면 완전히 같다.

<hr>

핸들러는 크게 콘텍스트를 저장하고 복원하는 역할을 담당하는 어셈블리어 코드(ISR, *Interrupt Service Routine*)와 실제 처리를 담당하는 C 코드 핸들러 함수로 구성된다.

> 핸들러 전체를 어셈블리어로 작성할 계획이라면 C 코드로 작성된 핸들러 함수는 필요하지 않다.
>
> 하지만, 어셈블리어로 작성하기에는 핸들러의 기능이 다소 복잡하므로, 어셈블리어 코드는 콘텍스트 처리만 담당하고 나머지 작업은 C 코드에서 처리하도록한다.

어셈블리어로 작성된 ISR 함수는 콘텍스트 저장과 복원을 담당하므로 이전 절에서 살펴보았던 코드와 같은 형태로 작성하면 된다.

하지만 ISR 함수는 인터럽트 처리를 위해 모두 16개를 작성해야 하는데 각 ISR 함수마다 콘텍스트를 저장하고 복원하는 코드를 반복해서 입력해야 한다.

> 반복되는 코드는 후에 유지보수에 문제가 생길 수 있다.

따라서 NASM의 매크로를 활용해 ISR함수를 작성해 보도록 하자.

#### 매크로를 활용해 작성한 ISR 함수

```assembly
; 콘텍스트를 저장하고 셀렉터를 교체하는 매크로
%macro KSAVECONTEXT 0	; 파라미터를 전달받지 않는 KSAVECONTEXT 매크로 정의
	; RBP 레지스터부터 GS 세그먼트 셀렉터까지 모두 스택에 삽입
	push rbp
	mov rbp, rsp
	push rax
	push rbx
	push rcx
	push rdx
	push rdi
	push rsi
	push r8
	push r9
	push r10
	push r11
	push r12
	push r13
	push r14
	push r15
	
	mov ax, ds		; DS 세그먼트 셀렉터와 ES 세그먼트 셀렉터는 스택에 직접
	push rax		; 삽입할 수 없으므로, RAX 레지스터에 저장한 후 스택에 삽입한다.
	mov ax, es
	push rax
	push fs
	push gs
	
	; 세그먼트 셀렉터 교체
	mov ax, 0x10	; AX 레지스터에 커널 데이터 세그먼트 디스크립터 저장
	mov ds, ax		; DS 세그먼트 셀렉터부터 FS 세그먼트 셀렉터까지 모두
	mov es, ax		; 커널 데이터 세그먼트로 교체
	mov gs, ax
	mov fs, ax
%endmacro

; 콘텍스트를 복원하는 매크로
%macro KLOADCONTEXT 0	; 파라미터를 전달받지 않는 KLOADCONTEXT 매크로 정의
	; GS 세그먼트 셀렉터부터 RBP 레지스터까지 모두 스택에서 꺼내 복원한다.
	pop gs
	pop fs
	pop rax
	mov es, ax		; ES 세그먼트 셀렉터와 DS 세그먼트 셀렉터는 스택에서 직접
	pop rax			; 꺼내 복원할 수 없으므로, RAX 레지스터에 저장한 뒤에 복원
	mov ds, ax
	
	pop r15
	pop r14
	pop r13
	pop r12
	pop r11
	pop r10
	pop r9
	pop r8
	pop rsi
	pop rdi
	pop rdx
	pop rcx
	pop rbx
	pop rax
	pop rbp
%endmacro

; #32, 타이머 ISR
kISRTimer:
	KSAVECONTEXT	; 콘텍스트를 저장한 뒤 셀렉터를 커널 데이터 디스크립터로 교체
	
	; 핸들러에 인터럽트 번호를 삽입하고 핸들러 호출
	mov rdi, 32
	call kCommonInterruptHandler
	
	KLOADCONTEXT
	iretq
	
; #33, 키보드 ISR
kISRKeyboard:
	KSAVECONTEXT
	
	; 핸들러에 인터럽트 번호를 삽입하고 핸들러 호출
	mov rdi, 33
	call kKeyboardHandler
	
	KLOADCONTEXT
	iretq
	
... 생략 ...
```

위 코드를 보면 C 코드로 작성된 핸들러 함수를 호출하기 전에 RDI 레지스터에 인터럽트 벡터를 삽입하는 것을 볼 수 있다.

이것은 핸들러 함수에 벡터 번호를 넘겨줌으로써 인터럽트가 발생한 PIC 컨트롤러에 EOI를 전송하려고 추가한 것이다.



< 인터럽트 핸들러 코드 >

다음은 위 코드에서 사용한 핸들러의 코드이다.

파라미터로 넘겨받은 벡터 번호를 이용하여 PIC 컨트롤러에 EOI를 전송하고, 화면의 왼쪽과 오른쪽 위에 벡터번호와 발생한 횟수를 출력하는 것을 볼 수 있다.

```C
void kCommonInterruptHandler(int iVectorNumber)
{
    char vcBuffer[] = "[INT:  , ]";
    static int g_iCommonInterruptCount = 0;
    //============================================================
    // 인터럽트가 발생했음을 알리려고 메시지를 출력하는 부분
    // 인터럽트 벡터를 화면 오른쪽 위에 2자리 정수로 출력
    vcBuffer[5] = '0' + iVectorNumber / 10;
    vcBuffer[6] = '0' + iVectorNumber % 10;
    // 발생한 횟수를 출력
    vcbuffer[8] = '0' + g_iCommonInterruptCount;
    g_iCommonInterruptCount = (g_iCommonInterruptCount + 1) % 10;
    kPrintString(70,0,0xB4, vcBuffer);
    //===========================================================
    
    // EOI 전송
    kSendEOIToPIC(iVectorNumber - 32); 	// 인터럽트 벡터는 32번부터 시작하므로
    									// 32를 빼서 IRQ 번호를 구한 후 EOI를 전송한다.
}

void kKeyboardHandler(int iVectorNumber)
{
    char vcBuffer[] = "[INT:  ,]";
    static int g_iKeyboardInterruptCount = 0;
    //============================================================
    // 인터럽트가 발생했음을 알리려고 메시지를 출력하는 부분
    // 인터럽트 벡터를 화면 오른쪽 위에 2자리 정수로 출력
    vcBuffer[5] = '0' + iVectorNumber / 10;
    vcBuffer[6] = '0' + iVectorNumber % 10;
    // 발생한 횟수 출력
    vcBuffer[8] = '0' + g_iKeyboardInterruptCount;
    g_iKeyboardInterruptCount = (g_iKeyboardInterruptCount + 1) % 10;
    kPrintString(0,0,0xB4, vcBuffer);
    //===========================================================
    
    //EOI로 전송
    kSendEOIToPIC(iVectorNumber - 32);       
}
```

<hr>



## IDT 테이블 수정

인터럽트와 예외 핸들러를 추가했으므로 IDT 테이블에 등록하는 핸들러 함수 역시 변경해야 한다.

핸들러를 변경하는 방법은 이전 코드의 *kDummyHandler* 부분을 추가한 핸들러로 대체하면 된다.

#### IDT 테이블의 핸들러를 새로운 핸들러로 대체하는 코드

```c
void kInitializeIDTTables(void)
{
    ...생략...
    
    //=====================================================
    // 예외 ISR 등록
    //=====================================================
    kSetIDTEntry(&(pstEntry[0]),kISRDivideError, 0x08, IDT_FLAGS_IST1,
                IDT_FLAGS_KERNEL, IDT_TYPE_IN);
    kSetIDTEntry(&(pstEntry[1]), kISRDebug, 0x08, IDT_FLAGS_IST1,
                 IDT_FLAGS_KERNEL,IDT_TYPE_INTERRUPT)
    ...생략...
   
   //=====================================================
   // 인터럽트 ISR 등록
   //=====================================================
    kSetIDTEntry(&(pstEntry[32]), kISRTimer, 0x08, IDT_FLAGS_IST1,
                IDT_FLAGS_KERNEL, IDT_TYPE_INTERRUPT);
    kSetIDTEntry(&(pstEntry[33]),kISRKeyboard, 0x08, IDT_FLAGS_IST1,
                IDT_FLAGS_KERNEL, IDT_TYPE_INTERRUPT);
    ...생략...;
  
    // 48번 이하의 핸들러는 기타 핸들러로 등록한다.
    for(i = 48; i < IDT_MAXENTRYCOUNT ; i++)
    {
        kSetIDTEntry(&(pstEntry[i]), kISRETCInterrupt, 0x08,
                    IDT_FLAGS_IST1, IDT_FLAGS_KERNEL, IDT_TYPE_INTERRUPT);
    }
}
```

<hr>

## 인터럽트 활성화와 비활성화

PIC 컨트롤러가 프로세서에 아무리 인터럽트 신호를 전달한다 해도 프로세서가 인터럽트를 무시하게 설정되어 있다면 핸들러가 수행되지 않는다.

프로세서의 RFLAGS 레지스터에는 인터럽트 발생 가능 여부를 설정하는 IF 비트가 있으며,<BR>해당 비트가 1로 설정되었을 때만 인터럽트가 발생한다.

따라서 OS가 인터럽트를 처리하려면 IF 비트를 직접 관리해야 한다.

x86 프로세서는 인터럽트를 활성화하고 비활성화하는 명령어를 제공한다.

만일 인터럽트를 활성화하고 싶다면 STI 명령어를,<br>비활성화하고 싶다면 CLI 명령어를 사용하면된다.

하지만, 프로세서는 인터럽트를 활성화 또는 비활성화하는 명령어만 지원할 뿐,<br>지금 프로세서의 상태가 어떤가에 대한 명령어는 별도로 지원하지 않는다.

대신 RFLAGS 레지스터를 스택에 저장하는 PUSHF 명령어를 제공하므로, 이를 간접적으로 확인할 수 있다.

#### 인터럽트 발생 가능 여부를 제어하고 RFLAGS 레지스터의 값을 읽는 어셈블리어 함수

```assembly
; 인터럽트를 활성화
;	PARAM :  없음
kEnableInterrupt:
	sti		; 인터럽트 활성화
	ret
	
; 인터럽트 비활성화
;	PARAM: 없음
kDisableInterrupt:
	cli		; 인터럽트 비활성화
	ret
	
; RFLAGS 레지스터를 읽어서 되돌려준다.
; PARAM: 없음
	pushfq		; RFLAGS 레지스터를 스택에 저장
	pop rax		; 스택에 저장된 RFLAGS 레지스터를 RAX 레지스터에 저장하여
				; 함수의 반환 값으로 설정
	ret
	
// C 함수 선언
void kEnableInterrupt(void);
void kDisableInterrupt(void);
QWORD kReadRFLAGS(void);
```







